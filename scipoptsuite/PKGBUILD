# Maintainer: Robert Schwarz <mail@rschwarz.net>
# Contributor: Johannes Schlatow <johannes.schlatow@googlemail.com>
# Contributor: Stephan Friedrichs <deduktionstheorem@googlemail.com>

pkgname='scipoptsuite'
pkgver='3.0.2'
pkgrel=2
pkgdesc="Toolbox for generating and solving optimization problems."
arch=('i686' 'x86_64')
url='http://scip.zib.de'
license=('LGPL3' 'custom:ZIB Academic License')
depends=('zlib' 'gmp' 'readline')
replaces=('ziboptsuite')
makedepends=('chrpath' 'doxygen' 'graphviz')
provides=('scip=3.0.2' 'soplex=1.7.2' 'zimpl=3.3.1' 'gcg=1.1.1' 'ug=0.7.2')
source=("http://scip.zib.de/download/release/${pkgname}-${pkgver}.tgz")
sha256sums=('4126f8b030e5423a373610bab38644d237c386f3ef9a820e3e33afb9acb86918')

build() {
    # Extract directory names from the $provides array.
    local _scip="${provides[0]//=/-}"
    local _soplex="${provides[1]//=/-}"
    local _zimpl="${provides[2]//=/-}"
    local _gcg="${provides[3]//=/-}"
    local _ug="${provides[4]//=/-}"

    cd "${srcdir}/${pkgname}-${pkgver}"

    # @TODO: shared lib with ZIMPL seems to be broken
    make ZIMPL=false SHARED=true scipoptlib

    make ZIMPL=true
    make ZIMPL=true gcg
    make ZIMPL=true ug

    cd "${srcdir}/${pkgname}-${pkgver}/${_scip}"
    make doc -j

    cd "${srcdir}/${pkgname}-${pkgver}/${_soplex}"
    make doc -j

    cd "${srcdir}/${pkgname}-${pkgver}/${_gcg}"
    make doc -j

    # Some files have permission 640.
    # @FIXME: Future versions might not require this line.
    chmod -R a+r "${srcdir}/${pkgname}-${pkgver}"
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make test
}

package_scipoptsuite() {
    # Extract directory names from the $provides array
    local _scip="${provides[0]//=/-}"
    local _soplex="${provides[1]//=/-}"
    local _zimpl="${provides[2]//=/-}"
    local _gcg="${provides[3]//=/-}"
    local _ug="${provides[4]//=/-}"

    cd "${srcdir}/${pkgname}-${pkgver}"

    cd "${srcdir}/${pkgname}-${pkgver}/${_scip}"
    make INSTALLDIR=${pkgdir}/usr/ installheaders installlibs

    # install the rest manually
    cd "${srcdir}/${pkgname}-${pkgver}"

    # A local RPATH is set, get rid of it.
    chrpath --delete ${_scip}/bin/scip
    chrpath --delete ${_soplex}/bin/soplex
    chrpath --delete ${_gcg}/bin/gcg
    chrpath --delete ${_ug}/bin/fscip

    #
    # Binaries
    #
    install -D -m755 ${_soplex}/bin/soplex "${pkgdir}/usr/bin/soplex"
    install -D -m755 ${_scip}/bin/scip "${pkgdir}/usr/bin/scip"
    install -D -m755 ${_zimpl}/bin/zimpl "${pkgdir}/usr/bin/zimpl"
    install -D -m755 ${_gcg}/bin/gcg "${pkgdir}/usr/bin/gcg"
    install -D -m755 ${_ug}/bin/fscip "${pkgdir}/usr/bin/fscip"

    #
    # Includes
    #
    mkdir -p "${pkgdir}/usr/include/"{soplex,zimpl}
    cp ${_soplex}/src/*.h "${pkgdir}/usr/include/soplex"
    cp ${_zimpl}/src/*.h "${pkgdir}/usr/include/zimpl"

    #
    # Libraries
    #
    cp -d ${_soplex}/lib/* "${pkgdir}/usr/lib"
    cp -d ${_zimpl}/lib/* "${pkgdir}/usr/lib"
    cp -d lib/libscipopt*.so "${pkgdir}/usr/lib/libscipopt.so"

    # Repair "missing links"
    # @FIXME: I hope this is not necessary in future versions!
    cd "${pkgdir}/usr/lib"
    ln -s -T libzimpl-*.a libzimpl.a
    cd "${srcdir}/${pkgname}-${pkgver}"

    #
    # Documentation
    #
    mkdir -p "${pkgdir}/usr/share/doc/${pkgname}/"{scip,soplex,zimpl,gcg,ug}
    cp -r ${_scip}/{CHANGELOG,release-notes,doc/html} "${pkgdir}/usr/share/doc/${pkgname}/scip/"
    cp -r ${_soplex}/{CHANGELOG,doc/html} "${pkgdir}/usr/share/doc/${pkgname}/soplex/"
    install -m644 ${_soplex}/src/simpleexample.cpp "${pkgdir}/usr/share/doc/${pkgname}/soplex/"
    cp -r ${_zimpl}/{CHANGELOG,README,doc,example} "${pkgdir}/usr/share/doc/${pkgname}/zimpl/"
    cp -r ${_gcg}/{CHANGELOG,README,doc/html} "${pkgdir}/usr/share/doc/${pkgname}/gcg/"
    cp -r ${_ug}/README "${pkgdir}/usr/share/doc/${pkgname}/ug/"

    #
    # License
    #
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
