# Maintainer: Robert Schwarz <mail@rschwarz.net>
# Contributor: Vladimir Chizhov <master@mirlord.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=qgo2-git
pkgver=2013.11.05.a1e827a
pkgrel=2
pkgdesc='Go client and full featured SGF editor; svn-snapshot of the next major version'
arch=('i686' 'x86_64')
url='https://github.com/pzorin/qgo'
license=('GPL')
depends=('qt5-base' 'qt5-multimedia')
makedepends=('git')
conflicts=('qgo' 'qgo2-cvs' 'qgo2-svn')
source=("$pkgname"::"git+https://github.com/pzorin/qgo.git")
md5sums=('SKIP')

pkgver() {
    cd "$pkgname"
    git log -1 --format="%cd.%h" --date=short | sed 's/-/./g'
}

build() {
  cd "$pkgname"
  qmake-qt5 QMAKE_CXXFLAGS="-fpermissive $CXXFLAGS" -o Makefile qgo.pro
  make
}

package() {
  cd "$pkgname"
  QTDIR=/usr make INSTALL_ROOT="$pkgdir/" install
}
